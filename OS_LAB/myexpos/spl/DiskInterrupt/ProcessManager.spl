
alias functionNum R1;
alias reqPID R2;

//Free User area Page
if(functionNum==2) then
	
	//Calling release Page

	multipush(R1,R2,R3);
	
	alias reqProcessTable R3;
	reqProcessTable = PROCESS_TABLE + reqPID*16; 
	
	R1 = 2;
	//Passing user area page number
	R2 = [reqProcessTable + 11 ];

	call MEMORY_MANAGER;

	multipop(R1,R2,R3);



endif;

//Exit Process`
if(functionNum==3) then
	
	alias reqProcessTable R3;
	reqProcessTable = PROCESS_TABLE + reqPID*16; 
	
	//Calling free user area page
	multipush (R1,R2,R3);
	
	R1 = 2;
	R2 = reqPID; 	
	
	call PROCESS_MANAGER;
	
	multipop(R1,R2,R3);
	
	//Caling free page table
	multipush (R1,R2,R3);
	
	R1 = 4;
	R2 = reqPID;	
	
	call PROCESS_MANAGER;
		
	multipop(R1,R2,R3);
	
	[reqProcessTable + 4] = "TERMINATED";
	
endif;

//Free Page Table
if(functionNum==4) then

	alias i R3;
	alias reqPageTable R4;
	reqPageTable = PAGE_TABLE_BASE + reqPID*20;
	i = 0;
	
	while(i<20) do
		//Here you check if page number is -1 to check validity	
		if([ reqPageTable +i]!=-1 ) then
		
			//Free that page if i is not 0 or 2
			if(i!=0&&i!=2) then
				
				//calling Releasepage
				multipush(R1,R2,R3,R4);
				
				R1 = 2;	
				R2 = [reqPageTable +i];
					
				call MEMORY_MANAGER;
				
				multipop(R1,R2,R3,R4);
				
			
			endif; 	
			
			[reqPageTable +i] = -1;
			[ reqPageTable +i+1] = "0000";
		
		endif;
		i = i+2;	
			
	endwhile;		
	

endif;

return;
